plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

// load key store for signing if key store file exists
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "de.tu_chemnitz.etit.sse.openstop"
    compileSdkVersion 35
    ndkVersion = "27.0.12077973"

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_11
      targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "de.tu_chemnitz.etit.sse.openstop"
        minSdkVersion 23
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    // properties of 'signingConfigs' are definied in /android/key.properties
    // a key.properties file needs to be created referencing the key(store) bound with verified app links
    signingConfigs {
        general {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    // due to using verified app links the app has to be signed with a certain key to be able to login to OSM
    // to enable login a certain 'signingConfigs' needs to be referenced for 'release' and 'debug'
    buildTypes {
        release {
            signingConfig signingConfigs.general
        }
        debug {
            // if key store file does not exist sign with debug keys
            signingConfig keystorePropertiesFile.exists() ? signingConfigs.general : signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}
